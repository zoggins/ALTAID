5 REM TINY BASIC TEST PROGRAM  - 4/22/18  DRH
10 CLS : PRINT '      TEST',\,'BASIC'
20 LET A=1:LET B=3:LET C=-100
30 D=A+B:E=C*B:F=B-A:G=C/4:H=(66+5)%11
40 PRINT '4=',#1,D,"  -300=",#4,E,"  100=",ABS(C)
50 PRINT '  2=',#1,F,"  -25=",#3,G,"  5=",#1,H
60 W="*" : PRINT #2,"ASCII(",W,") = * = ",$42
70 PRINT #1,A>B," = ",B<C," = ",B<=A," = ",C=B," = 0"
80 PRINT #1,A<B," = ",B>=C," = ",C<>B," = ",B<=A+2," = 1"
90 DIM J(5):DIM K(10)
100 FOR I = 1 TO 5
110 J(I) = J(I)+I*2:K(I*2) = J(I)+J(I):K(I*2-1)=I*4-2
120 NEXT I
130 FOR I = 1 TO 5
140 PR. "J(",#2,I,") = ",J(I)," = ",:X=I:GOSUB 400:PR. #2,X
150 NEXT I
160 FOR L = 2 TO 20 STEP 2
170 X = L/2
180 GOSUB 400
190 PRINT "K(",#2,L/2,") = ",K(L/2)," = ",X
200 NEXT L
205 POKE 8212,133 : POKE 8213,1 : REM RE-INIT SEED
210 PRINT RND(100),'  =      23'
220 RANDOMIZE
230 PRINT RND(100),'  <>     69'
240 INPUT "ENTER A NUMBER FROM 1 TO 100"M
245 IF (M > 100) PR. "TRY AGAIN" : GOTO 240
250 IF M < 25 GOTO 500
260 PR. "M IS GREATER THAN OR EQUAL TO 25"
270 IF M < 50 GOTO 510
280 PR. "M IS GREATER THAN OR EQUAL TO 50"
290 IF M < 75 GOTO 520
300 PR. "M IS GREATER THAN OR EQUAL TO 75"
310 IF M < 90 GOTO 530
320 IF M >= 90 PR. "M IS GREATER THAN 90"
330 IF M <> 42 GOTO 340
331 PR. "...AND M IS THE ANSWER TO LIFE"
332 PR. "...THE UNIVERSE"
333 PR. "...AND EVERYTHING"
340 @(100)=M : PRINT @(100)," = ",M,' = ',PEEK(15872-200)
350 S = FREE : PR. "MEMORY FREE =",SIZE," STARTING AT ",S," = 0X",
360 N = USR(4108,S) : PR. : REM CALL MONITOR PRINT WORD 
370 PR. "ENTER STRING :", : GET$(S)
380 PR. "STRING = ",$34,:PUT$(S):PR. $34," AND LENGTH = ",Z-1
390 STOP
400 REM SUBROUTINE
410 X = X * 2
420 RETURN
500 PRINT "M IS LESS THAN 25":GOTO 270
510 PRINT "M IS LESS THAN 50":GOTO 290
520 PRINT "M IS LESS THAN 75":GOTO 310
530 PRINT "M IS LESS THAN 90":GOTO 320
