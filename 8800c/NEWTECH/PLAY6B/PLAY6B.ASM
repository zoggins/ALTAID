; MICROPLAY REV.B DECEMBER 1977
; NEWTECH COMPUTER SYSTEMS INC.
; 230 Clinton Street
; Brooklyn, N.Y. 11201
;
; MICROPLAY STARTS AT THE BEGINHING OF THE
; MEMORY AREA DESIGNATED *SCORE* AND
; TRANSFERS INTO THE PLAY ROUTING A 1-BYTE
; PITCH PARAMETER AND A 2-BUTE DURATION
; PARAMETER.  (BITS A5,A4 AND A3 OF THE
; DURATION PARAMETER MOST SIGNIFICANT BYTE
; IS USED AS A FIELD TO SPECIFY WHICH
; AMPLITUDE ENVELOPE IS TO BE USED.)
; MICROPLAY CONTINUES TRANSFERRING NOTE
; PARAMETERS AND CALLING THE PLAY ROUTINE
; UNTIL A PITCH CONSTANT OF ZERO IS
; ENCOUNTERED WHICH INDICATES THE END OF
; THE MUSICAL SCORE.
;    THIS VERSION OF MICROPLAY WAS WRITTEN
; FOR A 8080 HAVING 0 WAIT STATES BUT CAN
; BE MODIFIED FOR 8080'S WITH WAIT STATES
; OR FOR Z80 PROCESSORS.
;
BEGIN		LXI		SP,STACK		;INIT. STACK POINTER.
INIT		LXI		H,SCORE			;INIT. SCORE POINTER.
			SHLD	PLACE
NEXT		LHLD	PLACE			;IF END OF SCORE THEN
			MVI		A,0				;LOOP HERE.
			CMP		M
HERE		JZ		HERE			;YOUR ENDING?
;									;ELSE TRANSFER
;									;PARAMETERS FOR NEXT
;									;NOTE OF SCORE INTO
;									;PLAY ROUTINE.
			MOV		A,M				;LOAD PITCH.
			STA		XFER2+1		
			STA		XFER4+1
			INX		H
			MOV		A,M				;LOAD NOTE DURATION
			STA		XFER1+1			;LSD.
			STA		XFER3+1
			INX		H
			MOV		A,M				;LOAD NOTE DURATION
			ANI		07H				;MASK 3 LSB'S.
			STA		XFER1+2			;MSD.
			STA		XFER3+2
			MOV		A,M				;GET MSD AGAIN
			ANI		38H				;MASK 3 BITS
			ADI		6FH				;ENVELOPE SPEC ADDRESS
			STA		PLAY+1
			INX		H
			SHLD	PLACE			;SAVE PLACE IN SCORE
;
			CALL	PLAY			;PLAY ONE NOTE.
			JMP		NEXT			;GO DO NEXT NOTE.
;
;
;
PLAY		LXI		H,TBL1			;INIT EVELOPE POINTER
			MVI		E,8				;INIT. SEGMENT COUNT
			MOV		A,M				;GET STARTING
;									;AMPLITUDE.
XFER1		LXI		B,LNGTH			;INIT. DURATION COUNT.
LOOP2		MOV		B,B				;WASTE TIME (WT2)
			JMP		XFER2			;(WT)
XFER2		MVI		D,PITCH			;INIT. PITCH CONSTANT.
			OUT		MODL6			;OUTPUT HALF WAVE TO
;									;MUSIC BOARD.
			INR		M				;WASTE MUCH TIME (WMT)
			DCR		M
			INR		M
			DCR		M
			INR		M
			DCR		M
LOOP3		DCR		D				;DELAY ACCORDING TO
			JNZ		LOOP3			;PITCH CONSTANT.
			XRA		M				;COMPLEMENT A.
			DCR		C				;COUNT DOWN DURATION #.
			JNZ		LOOP2
			DCR		B
			JNZ		XFER2
			OUT		MODL6
			INX		H				;SET UP NEXT SEGMENT.
			DCR		E				;DCR SEGMENT COUNT.
			RZ						;RETURN IF ALL
;									;SEGMENTS DONE.
XFER3		LXI		B,LNGTH
XFER4		MVI		D,PITCH
			MOV		A,M				;SET NEW AMPLITUDE.
			JMP		LOOP3
; ENVELOPE SPECIFICATION:
; TBL1 THRU TBL8 ARE 8 DIFFERENT NOTE
; EVELOPE SPECIFICATIONS, EACH ONE
; CONSISTING OF 8 AMPLITUDE SEGMENTS.
;
TBL1		DB		0FFH,0FFH,0E0H,0C0H	;ATTACH:#5
			DB		0A0H,90H,70H,50H
TBL2		DB		0,0,0,0				;REST:#R
			DB		0,0,0,0
TBL3		DB		0C0H,0FFH,90H,60H	;STACCATO:#S
			DB		35H,0,0,0
TBL4		DB		0C0H,0FFH,0FFH,0FFH	;LEGATO:#L
			DB		0FFH,0FFH,0A0H,90H	
TBL5		DB		70H,80H,60H,20H		;SOFT STACCATO:#1
			DB		0,0,0,0				
TBL6		DB		70H,80H,80H,80H		;SOFT LEGATO:#2
			DB		80H,80H,60H,40H
TBL7		DB		0A0H,0B7H,0D0H,0FFH	;"SHAPED":#3
			DB		0FFH,0FFH,0C7H,0A0H	
TBL8		DB		70H,85H,0A0H,0B5H	;CRESCENDO:#4
			DB		0D0H,0FFH,0FFH,0A0H
;
PLACE		DW		0					;SCORE POINTER.
SCORE		EQU		0100H				;YOUR SCORE LOCATION?
MODL6		EQU		24H					;YOUR OUTPUT PORT?
PITCH		EQU		0					;DUMMY EQUATE
LNGTH		EQU		0					;DUMMY EQUATE
STACK		EQU		$+10H
			END