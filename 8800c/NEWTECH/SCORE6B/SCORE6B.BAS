0010 REM SCORE6 REV.B JUNE 1978
0015 REM ADAPTED FROM SCORE68B REV.B  
0020 REM COPYRIGHT(C) 1978
0030 REM NEWTECH COMPUTER SYSTEMS,INC.
0040 REM ALL RIGHTS RESERVED
0050 REM 
0150 LET U=256 : REM U DEFINES SCORE AREA IN MEMORY
0160 LET K1=2^(1/12)
0170 LET K6=2.1 : REM TEMPO CONTROL
0175 LET E1=0 : REM ENVELOPE POINTER DEFAULT
0180 FOR V=1TO 1000
0181 READ Z$
0183 LET E=100 :REM PROCESS ENVELOPE POINTER. E WILL
0184 REM BE ADDED TO THE MOST SIGNIFICANT BYTE OF THE
0185 REM DURATION COUNT SO THAT BITS A6,A5 AND A3 ARE
0186 REM USED AS AN EVELOPE POINTER FIELD.
0187 REM
0188 LET A$=MID$(Z$,1,1)
0189 IF A$="5" THEN E=0     : REM ATTACK
0190 IF A$="R" THEN E=1*8  : REM REST
0191 IF A$="S" THEN E=2*8  : REM STACCATO
0192 IF A$="L" THEN E=3*8  : REM LEGATO
0193 IF A$="1" THEN E=4*8  : REM SOFT STACCATO
0194 IF A$="2" THEN E=5*8  : REM SOFT LEGATO
0195 IF A$="3" THEN E=6*8  : REM SHAPED
0196 IF A$="4" THEN E=7*8  : REM CRESCENDO
0197 IF E=100 THEN GOTO 200
0198 LET E1=E
0199 LET Z$=MID$(Z$,2)
0200 LET N=100
0225 LET A$=MID$(Z$,1,1)
0230 IF A$="A" THEN N=1
0240 IF A$="B" THEN N=3
0250 IF A$="C" THEN N=4
0260 IF A$="D" THEN N=6
0270 IF A$="E" THEN N=8
0280 IF A$="F" THEN N=9
0290 IF A$="G" THEN N=11
0300 IF A$="X" THEN GOTO 720
0310 IF N=100 THEN GOTO 760
0320 LET C=2
0330 LET M=100
0335 LET A$=MID$(Z$,2,1)
0340 IF A$="!" THEN M=N-1
0350 IF A$="#" THEN M=N+1
0360 IF A$=" " THEN M=N
0370 IF M=100 THEN GOTO 760
0380 LET C=3
0390 LET P=100
0395 LET A$=MID$(Z$,3,1)
0400 IF A$="1" THEN P=M
0410 IF A$="2" THEN P=M+12
0420 IF A$="3" THEN P=M+24
0430 IF P=100 THEN GOTO 760
0440 LET C=4
0450 LET T=100
0455 LET A$=MID$(Z$,4,1)
0460 IF A$="S" THEN T=16
0470 IF A$="E" THEN T=8
0480 IF A$="Q" THEN T=4
0490 IF A$="H" THEN T=2
0500 IF A$="W" THEN T=1
0510 IF T=100 THEN GOTO 760
0530 LET C=5
0540 IF MID$(Z$,5,1)="." THEN T=2*T/3
0550 REM CALCULATE CONSTANTS
0560 LET F1=220*(K1^(P-1))
0570 LET T1=10^6/(2*F1)
0580 LET K3=(T1-56.5)/7.5
0590 LET K4=F1/(K6*T)
0600 LET D3=INT(K4)  :  REM MAKE DURATION EVEN#
0610 LET D4=2*D3-2*INT(D3/2)
0620 LET D5=INT(D4/256) : REM CALC. 2 BYTES
0630 LET D6=D5+1+E1 : REM E1= ENVELOPE INFO.
0640 LET D7=D4-D5*256 : REM D7=LSB
0650 REM TRANSFER CONSTANTS TO SCORE AREA
0660 POKE (U+3*(V-1),INT(K3+.5))
0670 POKE (U+3*(V-1)+1,D7)
0680 POKE (U+3*(V-1)+2,D6)
0690 PRINT V;
0700 NEXT V
0710 STOP
0720 POKE ( U+3*(V-1),0)
0730 PRINT
0740 PRINT "SCORE COMPILATION COMPLETE"
0750 STOP
0760 PRINT "ERROR IN NOTE #";V
0770 PRINT "DATA STRING ";Z$
0780 PRINT "CHARACTER #";C
0790 STOP
8999 DATA X
9000 END