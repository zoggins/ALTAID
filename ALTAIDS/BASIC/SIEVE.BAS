5 REM SIEVE OF ERATOSTHENES
10 INPUT "ENTER NUMBER TO SEARCH TO " L
15 IF L > 3500 GOTO 140
20 FOR I=0 TO L: @(I)=0: NEXT I
25 GOSUB 165
28 PR."STARTING CALCULATION"
30 FOR N = 2 TO X
40 IF @(N) <> 0 GOTO 80
50 FOR K = N*N TO L STEP N
60 @(K) = 1
70 NEXT K
80 NEXT N
85 PR."DONE"
90 REM DISPLAY THE PRIMES
100 FOR N = 2 TO L
110 IF @(N) = 0 PR.#5,N,
120 NEXT N
130 STOP
140 PR."ERROR: MAXIMUM NUMBER IS 3500"
150 GOTO 10
160 REM SQUARE ROOT CALCULATION
165 X = L
170 Y = 1
180 IF (X-Y)<=0 RETURN
190 X = (X+Y)/2
200 Y=L/X
210 GOTO 180
220 RETURN
